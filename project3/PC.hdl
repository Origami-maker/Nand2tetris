// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/a/PC.hdl
/**
 * A 16-bit counter.
 * if      reset(t): out(t+1) = 0
 * else if load(t):  out(t+1) = in(t)
 * else if inc(t):   out(t+1) = out(t) + 1
 * else              out(t+1) = out(t)
 */
CHIP PC {
    IN in[16], reset, load, inc;
    OUT out[16];
    
    PARTS:
        Inc16(in=feedback, out=outInc);
        Mux16(a=feedback, b=outInc, sel=inc, out=outMux);
        Mux16(a=outMux, b=in, sel=load, out=outLoad);
        Mux16(a=outLoad, b=false, sel=reset, out=out1);
        Register(in=out1, load=true, out=feedback, out=out);
}